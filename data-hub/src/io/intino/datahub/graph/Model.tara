dsl Proteo

Concept:{0..1} Broker
	var string path
	var integer port
	var integer secondaryPort
	var object:{BrokerImplementation} implementation = empty
	Concept User
		var string name
		var string password
	Concept Pipe
    	var string origin
    	var string destination
    Concept Bridge
		Concept:{1..1} ExternalBus
			var string url
			var string user
			var string password
			var string sessionId
		var word:{outgoing incoming} direction = incoming
		var string[] topics

Concept:{0..1} Datalake
	var word:{Year Month Day Hour Minute} scale
	var string path
	Concept:Named Split > var string[] values
	Concept:{1..*}:AspectRequired Tank
		var string name
		Aspect TankType
        	sub Set
        		var Datalake.Split split = empty
        		var word:{Multivalued Singleton} type = Multivalued
        	sub Event

Concept:Named Message extends Schema
	Aspect Contextual
		var string context

Concept:Named MessageHub
	Concept:{0..1} Publish
		var Message[] messages
	Concept:{0..1} Subscribe
		var Message[] messages
!! Schemas allows to define how objects are. This objects can be provided as either parameter or response
Concept Schema is abstract
	var boolean multiple = false
	!! Attribute of the member
	Concept:Named:RequiresAspect Attribute extends Data
		var boolean isRequired = true
	has Schema
	Aspect ExtensionOf
    	var Schema parent


Concept Data is abstract
	Aspect List
	Aspect Type
		var string type is reactive private
		sub Real
			var double defaultValue = empty
			var string type = "Double" is reactive private
		sub Integer
			var integer defaultValue = empty
			var string type = "Integer" is reactive private
		sub LongInteger
			var long defaultValue = empty
			var string type = "Long" is reactive private
		sub Bool
			var boolean defaultValue = empty
			var string type = "Boolean" is reactive private
		sub Text
			var string defaultValue = empty
			var string type = "String" is reactive private
		sub Date
			var string format = "dd/MM/yyyy"
			var string type = "java.util.Date" is reactive private
		sub DateTime
			var string format = "dd/MM/yyyy HH:mm:ss"
			var string type = "java.time.Instant" is reactive private
		sub Word
			var string type = "java.lang.Enum" is reactive private
			var string[] values;
		sub File
			var string type = "io.intino.alexandria.Resource" is reactive private
			var word:{body path form header query} in = form
		sub Object
			var Schema schema
			var string type = @schemaName is reactive private
