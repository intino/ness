dsl Proteo

use Entities

Concept:{0..1} Broker
	var string path = empty
	var integer port = empty
	var integer secondaryPort = empty
	var object:{BrokerImplementation} implementation = empty
	Concept User
		var string name
		var string password
	Concept Pipe
		var string origin
		var string destination
	Concept Bridge
		Concept:{1..1} ExternalBus
			var string url
			var string user
			var string password
			var string sessionId
		var word:{outgoing incoming} direction = incoming
		var string[] topics

Concept:{0..1} Datalake
	var word:Scale scale
	var string path = empty
	Concept:{0..1} Backup
		var string path = empty
		Concept:{1..1} Cron
			var string pattern
			var string timeZone
	Concept:{0..1} Seal
		Concept:{1..1} Cron
			var string pattern
			var string timeZone
	Concept:{0..1} ProcessStatus
		var string name = "ProcessStatus" is private final
	Concept:{1..*}:AspectRequired Tank
		var word:Scale scale = empty
		var string qn = @qn is private reactive
		Aspect TankType
			sub Message
				var Model.Message message
				var boolean sortingRequired = true
			sub Measurement
				var Model.Sensor measurement
				var integer period
				var word:Scale periodScale;
			sub Resource
				var Model.Resource resourceEvent

Concept:Named Datamart
	var word:SnapshotScale scale
	var boolean saveOnExit = false
	var word:DayOfWeek firstDayOfWeek = MONDAY
	has Entity
	has Struct

Concept:Named Terminal
	Concept:{0..1} Bpm
		var string processStatusClass = "io.intino.alexandria.bpm.ProcessStatus" is private
	Concept:{0..1} Publish
		var Datalake.Tank.Message[] messageTanks
		var Datalake.Tank.Measurement[] measurementTanks = empty
		var Datalake.Tank.Resource[] resourceTanks = empty
	Concept:{0..1} Subscribe
		var Datalake.Tank.Message[] messageTanks
		var Datalake.Tank.Measurement[] measurementTanks = empty
		var Datalake.Tank.Resource[] resourceTanks = empty
	Concept:{0..1} Datamarts
		var Datamart[] list

Concept Namespace
	var string qn = @qn is private reactive
	has Namespace
	has Message
	has Sensor
	has Resource

Concept Sensor
	Concept:{1..*} Magnitude
		var string id
		Concept:Named Attribute
			var string value

Concept Resource

Concept:AvoidEventAttributes Message
	has:Named:RequiresAspect Attribute
	has Component
	Concept Has
		var Component element
		var boolean multiple = true
	Aspect ExtensionOf > var Message parent
	Aspect Assertion

Concept Component
	var boolean multiple = true
	has Attribute
	has Component
	Concept Has
		var Component element
		var boolean multiple = true
	Aspect ExtensionOf > var Component parent

Concept:Named:RequiresAspect Attribute extends Data is component

Concept Data is abstract component
	Aspect List
	Aspect Type
		var string type is reactive private
		var string primitive = 'self.type()' is reactive private
		var integer:{bits} size = 0 bits
		var integer:{bits} maxSize = 64 bits is private
		sub Real
			var double defaultValue = 0
			var string type = "Double" is reactive private
			var string primitive = "double" is reactive private
			var integer:{bits} size = 64 bits
		sub Integer
			var integer defaultValue = 0
			var string type = "Integer" is reactive private
			var string primitive = "int" is reactive private
			var integer:{bits} size = 32 bits
			var integer:{bits} maxSize = 32 bits is final private
		sub LongInteger
			var long defaultValue = 0
			var string type = "Long" is reactive private
			var string primitive = "long" is reactive private
			var integer:{bits} size = 64 bits
			var integer:{bits} maxSize = 64 bits is final private
		sub Id
			var long defaultValue = 0
			var string type = "Long" is reactive private
			var string primitive = "long" is reactive private
			var integer:{bits} size = 64 bits
		sub Bool
			var boolean defaultValue = false
			var string type = "Boolean" is reactive private
			var string primitive = "boolean" is reactive private
			var integer:{bits} size = 2 bits is final
		sub Text
			var string defaultValue = empty
			var string type = "String" is reactive private
		sub Date
			var string format = "dd/MM/yyyy"
			var string type = "java.time.LocalDate" is reactive private
			var integer:{bits} size = 16 bits
		sub DateTime
			var string format = "dd/MM/yyyy HH:mm:ss"
			var string type = "java.time.Instant" is reactive private
			var word:{Millis Seconds Minutes Hours} precision = Millis
			var integer:{bits} size = 64 bits
		sub Word
			var string type = "java.lang.Enum" is reactive private
			var string[] values;
			var integer:{bits} size = 16 bits